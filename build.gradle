group 'protobuff'
version '0.0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.osdetector'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.4.0'
    }
}

def tcnative_classifier = osdetector.classifier;
if (osdetector.os == "linux" && osdetector.release.isLike("fedora")) {
    tcnative_classifier += "-fedora";
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.grpc:grpc-netty:1.0.1'
    compile 'io.grpc:grpc-protobuf:1.0.1'
    compile 'io.grpc:grpc-stub:1.0.1'
    compile 'io.netty:netty-tcnative:1.1.33.Fork23:' + tcnative_classifier
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.2'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.1'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.test.protobuf.HelloServer'
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// hack to enable syntax highlighting in IDE
// it will compile without this
sourceSets {
    generated {
        java.srcDir "${buildDir}/generated/source/proto/main/grpc"
        java.srcDir "${buildDir}/generated/source/proto/main/java"
    }
}